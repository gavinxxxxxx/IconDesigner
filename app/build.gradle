apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'

def static autoVersionCode() {
    return Integer.parseInt(new Date().format("yyMMdd", TimeZone.getTimeZone("UTC")))
}

def static autoVersionName() {
    return new Date().format("0.yy.MM.dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "me.gavin.icon.designer"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode autoVersionCode()
        versionName autoVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "zh", "en"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            versionNameSuffix '.debug'
            buildConfigField "boolean", "LOG_DEBUG", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            versionNameSuffix '.release'
            buildConfigField "boolean", "LOG_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

andResGuard {
    // @link {https://github.com/shwenzhang/AndResGuard/blob/master/README.zh-cn.md}
    mappingFile = null
    use7zip = false
    useSign = true
    keepRoot = false
    whiteList = [
            // nothing
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.10'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation project(':menu.multilevel')
    implementation project(':color.picker')

    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    implementation 'com.github.SilenceDut:jpinyin:v1.0'

    implementation 'com.google.firebase:firebase-ads:11.6.2'
}

apply plugin: 'com.google.gms.google-services'